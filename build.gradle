plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'snw.rfm'
version = '0.1.0'

repositories {
    mavenCentral()
    maven {
        name = "JitPack"
        url = "https://jitpack.io/"
    }
}

def lombok = "org.projectlombok:lombok:1.18.32"
dependencies {
    compileOnly "com.google.guava:guava:31.1-jre"
    compileOnly "org.jetbrains:annotations:24.1.0"
    if (parent != null) { // should only be true if we're in the plugin project
        api parent.project(":api:common")
    } else { // or maybe we're compiling outside the plugin project
        api "com.github.SNWCreations.RunForMoney3API:common:main-SNAPSHOT"
    }

    compileOnly lombok
    annotationProcessor lombok
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}